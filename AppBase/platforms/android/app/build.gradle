apply plugin: 'com.android.application'

android {
  namespace 'com.rihen.appbase'
  compileSdkVersion 31
  ndkVersion '25.1.8937393'

  defaultConfig {
      applicationId = 'com.rihen.appbase'
      minSdkVersion 21
      targetSdkVersion 31
  }
  
  externalNativeBuild {
      ndkBuild {
          path '../../../../MKDROID_Android.mk' // Chemin vers votre fichier .mk
      }
  }

  buildDir = project.findProperty('buildDir') ?: "$rootDir/build"

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def apkPath = project.findProperty('apkPath') ?: "AppBase"
            def apkName = project.findProperty('apkName') ?: "AppBase-1.0"
            def variantSuffix = variant.buildType.name == "release" ? "release" : "debug"
            //output.outputFileName = "${apkPath}/${apkName}-${variant.versionName}-${variantSuffix}.apk" // Nom de l'APK
            output.outputFileName = "${apkName}-${variant.versionName}-${variantSuffix}.apk" // Nom de l'APK

            // Ajouter une tâche pour copier l'APK dans le répertoire de destination
            variant.assemble.doLast {
                def buildDir = output.outputFile.parent  // Répertoire où l'APK est généré
                def sourceApk = new File(buildDir, output.outputFileName)
                def destinationDir = new File("$buildDir/$apkPath")

                // Créer le répertoire de destination s'il n'existe pas
                if (!destinationDir.exists()) {
                    destinationDir.mkdirs()
                }

                // Copier l'APK
                def destinationApk = new File(destinationDir, sourceApk.name)
                project.copy {
                    from sourceApk
                    into destinationDir
                }
            }
        }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
}
